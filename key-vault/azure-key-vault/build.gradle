plugins {
  id "java-library"
  id "application"
}

application {
  startScripts.enabled = false
}

dependencyCheck {
  failBuildOnCVSS = 11
}

configurations.all {
  exclude group: "stax"
  exclude module: "javax.mail"
  exclude group: "jakarta.json"
}

def nettyVersion = "4.1.76.Final"

dependencies {
  // set version of nearly all netty modules this way
  implementation platform('io.projectreactor:reactor-bom:2020.0.18')
  implementation 'io.projectreactor.netty:reactor-netty'
  implementation 'io.projectreactor.netty:reactor-netty-core'
  implementation 'io.projectreactor.netty:reactor-netty-http'
  // set the versions of any that are still causing dependency check issues
  implementation "io.netty:netty-transport-native-kqueue:$nettyVersion"

  implementation project(":config")
  implementation project(":key-vault:key-vault-api")
  implementation "jakarta.annotation:jakarta.annotation-api"
  implementation "com.sun.activation:jakarta.activation"

  implementation ("com.azure:azure-security-keyvault-secrets:4.2.3")
  implementation("com.azure:azure-identity:1.3.5")
  implementation("com.azure:azure-core:1.19.0")

  testImplementation "org.glassfish:jakarta.json"

  constraints {
    implementation("com.fasterxml.jackson.core:jackson-databind:$jacksonDatabindVersion") {
      because 'databind less than 2.13.2.2 has a bug'
    }
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifact distZip
      artifact distTar
    }
  }
}
